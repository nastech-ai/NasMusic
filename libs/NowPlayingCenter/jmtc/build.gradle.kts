import java.io.ByteArrayOutputStream

plugins {
    java
    alias(libs.plugins.maven.publish)
}

val groupVal = "io.github.selemba1000"
val nameVal = "JavaMediaTransportControls"
val versionVal = "0.0.3"

repositories {
    mavenCentral()
}

java{
    withSourcesJar()
}

dependencies {
    testImplementation(platform("org.junit:junit-bom:5.10.2"))
    testImplementation("org.junit.jupiter:junit-jupiter")
    testRuntimeOnly("org.junit.platform:junit-platform-launcher")
    implementation("net.java.dev.jna:jna:5.14.0")
    implementation("com.github.hypfvieh:dbus-java-core:5.0.0")
    implementation("com.github.hypfvieh:dbus-java-transport-native-unixsocket:5.0.0")
}

tasks.withType<Test>().configureEach {
    useJUnitPlatform()
}

tasks.javadoc {
    if (JavaVersion.current().isJava9Compatible) {
        (options as StandardJavadocDocletOptions).addBooleanOption("html5", true)
    }
}

tasks.register<NativeCompile>("build_x86_debug") {
    inputs.property("platform" , "Win32")
    inputs.property("configuration" , "Debug")
}

tasks.register<NativeCompile>("build_x86_release") {
    inputs.property("platform" , "Win32")
    inputs.property("configuration" , "Release")
}

tasks.register<NativeCompile>("build_x64_debug") {
    inputs.property("platform" , "x64")
    inputs.property("configuration" , "Debug")
}

tasks.register<NativeCompile>("build_x64_release") {
    inputs.property("platform" , "x64")
    inputs.property("configuration" , "Release")
}

tasks.register("build_debug"){
    group = "build native"
    dependsOn("build_x64_debug","build_x86_debug")
}

tasks.register("build_release"){
    group = "build native"
    dependsOn("build_x64_release","build_x86_release")
}

tasks.register<Delete>("clean_native"){
    group = "build native"
    delete(layout.projectDirectory.dir("src/main/sources").files(".build","bin"))
}

tasks.register<Copy>("copy_x86_debug"){
    from("src/main/sources/bin/Debug/x86/SMTCAdapter.dll")
    into("src/main/resources/win32-x86")
    dependsOn("build_debug")
}
tasks.register<Copy>("copy_x86_release"){
    from("src/main/sources/bin/Release/x86/SMTCAdapter.dll")
    into("src/main/resources/win32-x86")
    dependsOn("build_release")
}
tasks.register<Copy>("copy_x64_debug"){
    from("src/main/sources/bin/Debug/x64/SMTCAdapter.dll")
    into("src/main/resources/win32-x86-64")
    dependsOn("build_debug")
}
tasks.register<Copy>("copy_x64_release"){
    from("src/main/sources/bin/Release/x64/SMTCAdapter.dll")
    into("src/main/resources/win32-x86-64")
    dependsOn("build_release")
}

tasks.register("copy_debug"){
    group = "build native"
    dependsOn("copy_x86_debug","copy_x64_debug")
}
tasks.register("copy_release"){
    group = "build native"
    dependsOn("copy_x86_release","copy_x64_release")
}

abstract class NativeCompile : DefaultTask() {

    @get:InputFiles
    val source = arrayOf("src/main/sources/SMTCAdapter.vcxproj","src/main/sources/SMTCAdapter.cpp","src/main/sources/SMTCAdapter.h").map { File(it) }

    @get:OutputDirectories
    val output = arrayOf(File("src/main/sources/.build"),File("src/main/sources/bin"))

    @TaskAction
    fun compile() {
        val platform = inputs.properties["platform"]
        val configuration = inputs.properties["configuration"]
        val source = inputs.files
        val command = "MSBuild.exe -m /property:Platform=${platform} /property:Configuration=${configuration}"
        val stream = ByteArrayOutputStream()
        providers.exec {
            workingDir = source.single { it.extension == "vcxproj" }.parentFile
            commandLine(command.split(" "))
            standardOutput = stream
        }
    }

}

mavenPublishing {
    println("Setting directory for publishing:")
    publishToMavenCentral(automaticRelease = false)
    signAllPublications()
    coordinates("org.nasmusic", "jmtc", libs.versions.library.get())
    pom {
        name.set("JMTC")
        description.set("JMTC forked version")
        inceptionYear.set("2025")
        url.set("https://github.com/nastechai/NowPlayingCenter")
        licenses {
            license {
                name.set("MIT License")
                url.set("https://github.com/nastechai/NowPlayingCenter/blob/master/LICENSE")
                distribution.set("https://github.com/nastechai/NowPlayingCenter/blob/master/LICENSE")
            }
        }
        developers {
            developer {
                id.set("nastechai")
                name.set("Nguyen Duc Tuan Minh")
                url.set("https://github.com/nastechai/")
            }
        }
        scm {
            url.set("https://github.com/nastechai/NowPlayingCenter")
            connection.set("scm:git:git://github.com/nastechai/NowPlayingCenter.git")
            developerConnection.set("scm:git:ssh://git@github.com/nastechai/NowPlayingCenter.git")
        }
    }
}